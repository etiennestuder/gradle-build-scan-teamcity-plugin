plugins {
    id 'com.gradle.build-scan' version '1.16'
    id 'nu.studer.plugindev' version '1.0.12'
    id 'nu.studer.credentials' version '1.0.7'
    id 'groovy'
}

group = 'nu.studer'
version = '1.1-DEV'

buildScan {
    licenseAgreementUrl 'https://gradle.com/terms-of-service'
}

repositories {
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
}

dependencies {
    compileOnly 'com.gradle:build-scan-plugin:1.16'

    testCompile('org.spockframework:spock-core:1.3-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'io.ratpack:ratpack-groovy-test:1.7.5', {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.10.0'
}

plugindev {
    pluginId = 'nu.studer.build-scan.teamcity'
    pluginDescription 'Gradle plugin for build scans that notifies TeamCity when a build scan is published during a build.'
    pluginImplementationClass 'nu.studer.gradle.buildscan.teamcity.TeamCityBuildScanPlugin'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'teamcity', 'build scan'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-build-scan-teamcity-plugin'
    projectInceptionYear '2019'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = false
}

test {
    environment('TEAMCITY_VERSION', '2019.1')
}

pluginUnderTestMetadata {
    // because of weird classloading when testing plugins via TestKit we need to explicitly ensure that the build
    // scan plugin is on our plugin classpath at test runtime
    pluginClasspath = pluginClasspath + configurations.compileOnly
}
